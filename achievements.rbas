#include "achievements.bi"
#include "reload.bi"
#include "steam.bi"

using Reload

' Local functions
declare function type_name_to_enum(name as string) as AchievementType
declare function mark_tag_as_seen(id as integer, tag as integer, is_triggered as boolean) as boolean
declare function is_complete(id as integer) as boolean
declare function needs_progress_update(id as integer) as boolean

dim shared next_id as integer
redim shared achievements(0) as AchievementDefinition
redim shared achievement_progress(0) as AchievementProgress

' -- public api --

sub load_achievements(file_path as string)
    free_achievements

    DIM doc as Docptr
    doc = LoadDocument(file_path, LoadOptions.optIgnoreMissing)
    if doc = null then return

    dim cheevos as Nodeptr = DocumentRoot(doc)
    if cheevos = null then
        FreeDocument(doc)
        return
    end if

    dim num_achievements as integer = CountChildren(cheevos, "achievement")
    ' debug "achievements: loading " & num_achievements & " from disk"
    
    redim achievements(num_achievements)
    redim achievement_progress(num_achievements)

    dim i as integer = 0

    READNODE cheevos
        WITHNODE cheevos."achievement" as cheevo   
            ' debug "achievements: loading achievement #" & i
            with achievements(i)
                .id = GetInteger(cheevo)
                .name = cheevo."name".string
                .achievement_type = type_name_to_enum(cheevo."type".string)
                .max_value = cheevo."max_value".integer
                .progress_interval = cheevo."progress_interval".integer
                .latching = cheevo."latching".exists
                .steam_id = cheevo."steam_id".string

                dim tag as Nodeptr = FirstChild(cheevo."tags".ptr, "tag")
                while tag <> null
                    v_append .tags, GetInteger(tag)
                    tag = NextSibling(tag, "tag")
                wend
            end with
            ' debug "achievements: Finished loading achievement #" & i & ", " & achievements(i).name
            i += 1
        END WITHNODE
    END READNODE

    FreeDocument(doc)
end sub

sub free_achievements
    redim achievements(0)
    redim achievement_progress(0)

    next_id = 1
end sub

sub achievement_tag_notify(id as integer, is_triggered as boolean)
    for i as integer = 0 to ubound(achievements)
        if mark_tag_as_seen(i, id, is_triggered) then
            if is_complete(id) then
                ' reward the achievement
                reward_achievement achievements(id).steam_id
            elseif needs_progress_update(id) then
                ' ping user with progress
                notify_achievement_progress achievements(id).steam_id, achievement_progress(id).value, achievements(id).max_value
            end if
        end if
    next
end sub

sub achievement_evaluate_tags()
    
end sub

' -- members for AchievementDefinition --

constructor AchievementDefinition
    id = 0
    name = ""
    achievement_type = AchievementType.flag
    max_value = 0
    progress_interval = 0
    latching = false
    steam_id = ""

    v_new tags
end constructor

destructor AchievementDefinition
    v_free tags
end destructor

' -- members for AchievementProgress --

constructor AchievementProgress
    id = 0
    value = 0
    v_new seen_tags
end constructor

destructor AchievementProgress
    v_free seen_tags
end destructor

' -- internal helpers --

function type_name_to_enum(name as string) as AchievementType
    select case name
        case "flag"
            return AchievementType.flag
        case "count"
            return AchievementType.count
        case else
            debug "achievements: unknown achievement type string: " & name
            return AchievementType.flag
    end select
end function

function type_enum_to_name(typ as AchievementType) as string
    select case typ
        case AchievementType.flag
            return "flag"
        case AchievementType.count
            return "count"
        case else
            debug "achievements: unknown achievement type enum: " & typ
            return "flag"
    end select
end function

function mark_tag_as_seen(id as integer, tag as integer, is_triggered as boolean) as boolean
    with achievements(id)
        ' ... do they care about this tag?
        dim ix as integer = v_find (.tags, tag)
        ' if they do,
        if ix <> -1 then
            ix = v_find(achievement_progress(id).seen_tags, tag)
            if is_triggered and ix = -1 then
                v_append achievement_progress(id).seen_tags, tag
                if .achievement_type = AchievementType.count and achievement_progress(id).value < .max_value then
                    achievement_progress(id).value += 1
                end if
                return true
            elseif .latching = false andalso is_triggered = false andalso ix <> -1 then
                v_remove achievement_progress(id).seen_tags, tag
                ' note: disabling a tag does _not_ decrement the value
                return true
            end if
        end if
    end with

    return false
end function

function is_complete(id as integer) as boolean
    dim achievement as AchievementDefinition ptr = @achievements(id)
    dim progress as AchievementProgress ptr = @achievement_progress(id)

    for ix as integer = 0 to v_len(achievement->tags)
        dim tag as integer = *v_at(achievement->tags, ix)
        if v_find(progress->seen_tags, tag) = 0 then
            return false
        end if
    next

end function

' call this _after_ checking is_complete!
function needs_progress_update(id as integer) as boolean
    dim achievement as AchievementDefinition ptr = @achievements(id)
    dim progress as AchievementProgress ptr = @achievement_progress(id)

    return achievement->achievement_type = AchievementType.count _
        andalso achievement->progress_interval > 0 _
        andalso progress->value > 0 _
        andalso (progress->value mod achievement->progress_interval) = 0
end function